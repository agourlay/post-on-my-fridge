description = "Post on my fridge"

apply plugin: 'java'
defaultTasks 'clean','gaeDownloadSdk', 'gaeExplodeWar', 'minifyFridgeCss','minifyFridgeJs','gaeRun'

buildscript {
	repositories { 
		mavenCentral()
		add(new org.apache.ivy.plugins.resolver.URLResolver()) {
			name = 'GitHub'
			addArtifactPattern 'http://cloud.github.com/downloads/[organisation]/[module]/[module]-[revision].[ext]'
		}
	}

	dependencies {
		classpath 'com.eriwen:gradle-js-plugin:1.1'
		classpath 'com.eriwen:gradle-css-plugin:1.1.2'
		classpath 'bmuschko:gradle-gae-plugin:0.8' 
	}
}

apply plugin: 'js'

task combineFridgeJs(type: com.eriwen.gradle.js.tasks.CombineJsTask) {
	source = [
		"${projectDir}/src/main/webapp/js/util/tools.js",
		"${projectDir}/src/main/webapp/js/app.js",
		"${projectDir}/src/main/webapp/js/util/jquery-ui-ember.js",
		"${projectDir}/src/main/webapp/js/model/postModel.js",
		"${projectDir}/src/main/webapp/js/view/postView.js",
		"${projectDir}/src/main/webapp/js/controller/fridgeController.js",
		"${projectDir}/src/main/webapp/js/view/fridgeView.js",
		"${projectDir}/src/main/webapp/js/model/messageModel.js",
		"${projectDir}/src/main/webapp/js/view/messageView.js",
		"${projectDir}/src/main/webapp/js/view/chatView.js",
		"${projectDir}/src/main/webapp/js/controller/chatController.js",
		"${projectDir}/src/main/webapp/js/router.js"
	]
	dest = file("${buildDir}/exploded-war/js/fridge.js")
}

task minifyFridgeJs(type: com.eriwen.gradle.js.tasks.MinifyJsTask, dependsOn: 'combineFridgeJs') {
	source = combineFridgeJs
	dest = file("${buildDir}/exploded-war/js/fridge.min.js")
}

apply plugin: 'css'

css.source {
    app {
        css {
            srcDir "${projectDir}/src/main/webapp/css/"
            include "*.css"
        }
    }
}

task combineFridgeCss(type: com.eriwen.gradle.css.tasks.CombineCssTask) {
	source = css.source.app.css.files
	dest = file("${buildDir}/exploded-war/css/fridge.css")
}

task minifyFridgeCss(type: com.eriwen.gradle.css.tasks.MinifyCssTask, dependsOn: 'combineFridgeCss') {
	source = combineFridgeCss
	dest = file("${buildDir}/exploded-war/css/fridge.min.css")
}

apply plugin: 'gae'

gae {
	httpPort = 8888
	stopKey = "stop"
	disableUpdateCheck = true
	downloadSdk = true
	appcfg { email = 'arnaud.gourlay@gmail.com' }
	jvmFlags = ['-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']
}

dependencies {
	gaeSdk  'com.google.appengine:appengine-java-sdk:1.7.4'
	compile 'com.googlecode.objectify:objectify:4.0b1'
	compile 'com.google.guava:guava:14.0-rc1'
	compile 'org.projectlombok:lombok:0.11.6'
	compile 'com.sun.jersey:jersey-core:1.17'
	compile 'com.sun.jersey:jersey-server:1.17'
	compile 'com.sun.jersey:jersey-servlet:1.17'
	compile 'com.fasterxml.jackson.core:jackson-core:2.1.3'
	compile 'com.fasterxml.jackson.core:jackson-databind:2.1.3'
	compile 'com.fasterxml.jackson.core:jackson-annotations:2.1.2'
	compile	'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.1.2'
	compile 'joda-time:joda-time:2.1'
	testCompile 'junit:junit:4.11'
}